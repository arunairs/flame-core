import cn.blinkmind.flame.gradle.TaskGroup
import cn.blinkmind.flame.gradle.external.Mongo
import com.bmuschko.gradle.docker.tasks.image.DockerPullImage

task mongoEnv(group: TaskGroup.MONGO, dependsOn: ['componentEnv']) {
    ext.addMongo = { String name ->
        componentEnv.add(name, new Mongo(name, project,
                new Mongo.Settings(
                        homeDir: env.getRequiredString('MONGO_HOME'),
                        host: env.getRequiredString('MONGO_HOST'),
                        port: env.getRequiredInteger('MONGO_PORT'),
                        dataDir: env.getRequiredString('MONGO_DATADIR'),
                        database: env.getRequiredString('MONGO_DATABASE')
                )))
    }

    ext.getMongo = { String name ->
        return componentEnv.get(name)
    }

    if (env.getString('MONGO_HOME')) {
        addMongo('build')
    }
}

task startMongo(group: TaskGroup.MONGO, dependsOn: ['mongoEnv']) {
    doLast {
        Mongo mongo = mongoEnv.getMongo('build')
        mongo.start()
    }
}

task stopMongo(group: TaskGroup.MONGO, dependsOn: ['mongoEnv']) {
    doLast {
        Mongo mongo = mongoEnv.getMongo('build')
        mongo.stop()
    }
}

task pullMongoImage(type: DockerPullImage, group: TaskGroup.MONGO) {
    repository = 'build'
    tag = 'latest'
}
