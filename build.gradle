import cn.blinkmind.flame.gradle.Environment

buildscript {
    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public' }
        maven { url 'http://repo.spring.io/plugins-release' }
    }

    ext {
        libs = [
                spring : [
                        mvc : [version: '4.3.7.RELEASE'],
                        boot: [version: '1.5.2.RELEASE']
                ],
                jackson: [
                        version: '2.8.7'
                ],
                commons: [
                        lang     : [version: '3.5'],
                        codec    : [version: '1.10'],
                        validator: [version: '1.6'],
                        beanutils: [version: '1.9.3']
                ],
                lombok : [
                        version: '1.16.14'
                ]
        ]
    }

    dependencies {
        classpath group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: libs.spring.boot.version
        classpath group: 'org.springframework.build.gradle', name: 'propdeps-plugin', version: '0.0.7'
        classpath group: 'com.bmuschko', name: 'gradle-docker-plugin', version: '3.0.4'
    }
}

def env = Environment.getInstance().addProperties(rootProject.file('gradle.properties').newDataInputStream())
rootProject.ext.env = env

allprojects { project ->
    apply plugin: 'java'
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    version = archiveVersion

    project.tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public' }
        jcenter()
    }

    dependencies {
        testCompile group: 'org.testng', name: 'testng', version: '6.11'
    }

    test {
        useTestNG()
        enabled = true
    }

    task s1tests(type: Test) {
        useTestNG() {
            includeGroups 's1'
        }

        //jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5007'
    }
}